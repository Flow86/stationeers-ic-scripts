# Automatic Room Temperature Control (Cooling&Heating)
# written by Flow86
#
# * Use the Dial to set the temperature
# * has a hysteresis

# if temp > curtemp+chyst -> cooling start
# if temp < curtemp-chyst -> cooling stop
# if temp < curtemp-lowtemp -> heating start
# if temp >= curtemp-chyst -> heating stop

define cHashWallCooler -739292323
define cHashLiquidWallCooler -1369060582

define cHashWallHeater 24258244

define cMaxTemperature 40         # °C
define cDefaultTemperature 21     # °C
define cHysteresisTemperature 0.5 # K
define cLowTemperatureHeating 3   # K

alias dGasSensor d0
alias dDialTemperature d1

alias rWallCooler r0
alias rWallHeater r1
alias rSetTemperature r2
alias rTemperature r3

alias rTempHigher r4
alias rTempLower r5
alias rCoolingTemp r6

init:
move rWallCooler 0
move rWallHeater 0

s dDialTemperature Mode cMaxTemperature
#s dDialTemperature Setting cDefaultTemperature

jal update_state

start:
l rSetTemperature dDialTemperature Setting
add rSetTemperature rSetTemperature 273

l rTemperature dGasSensor Temperature

add rSetTemperature rSetTemperature cHysteresisTemperature
sgt rTempHigher rTemperature rSetTemperature

sub rSetTemperature rSetTemperature cHysteresisTemperature
sub rSetTemperature rSetTemperature cHysteresisTemperature
slt rTempLower rTemperature rSetTemperature

# if temp > set+hyst step into
breqz rTempHigher 2
move rWallCooler 1

# if temp < set-hyst step into
breqz rTempLower 2
move rWallCooler 0

add rSetTemperature rSetTemperature cHysteresisTemperature
sge rTempHigher rTemperature rSetTemperature

sub rSetTemperature rSetTemperature cLowTemperatureHeating
slt rTempLower rTemperature rSetTemperature

# if temp >= set step into
breqz rTempHigher 2
move rWallHeater 0

# if temp < set-low step into
breqz rTempLower 2
move rWallHeater 1


jal update_state
yield
j start

update_state:
sb cHashWallCooler On rWallCooler
sb cHashLiquidWallCooler On rWallCooler

sb cHashWallHeater On rWallHeater

s db Setting rWallCooler
j ra