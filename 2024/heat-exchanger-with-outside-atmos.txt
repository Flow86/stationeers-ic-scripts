# Controller for filling a coolant tank
# with outside atmosphere
# GasSensor for outside Temp
# Dial for Target Temperature
# Vent -> Tank              -> HE
#            ^ <- Pump (On) <- |
# Passive Vent <- Pump (d4) <- |
#

define cMaxTemperature 60  # Â°C
define cTankPressure 20    # MPa
define cOverTemperature 10 # K
define cOverPressure 2000  # Pa
define cMinTempOut 253     # -20C in K

alias dGasSensor d0
alias dTank d1
alias dDial d2

alias dVent d3
alias dPumpToOutside d4
alias dDiode d5

alias rVent r0
alias rPumpToOutside r1

alias rOutsideTemp r3
alias rTankTemp r4
alias rTankPressure r5
alias rSetPressure r6
alias rSetTemperature r7

alias rOutTempHigh r8
alias rTankPressureLow r9
alias rTankPressureHigh r10
alias rTankTemperatureHigh r11

init:
move rVent 0
move rPumpToOutside 0
jal update_state

s dDial Mode cMaxTemperature

start:
l rOutsideTemp dGasSensor Temperature
l rTankPressure dTank Pressure
l rTankTemp dTank Temperature
l rSetTemperature dDial Setting
add rSetTemperature rSetTemperature 273

# outside temperature high enough?
sgt rOutTempHigh rOutsideTemp cMinTempOut

# tank temperature too high?
sgt rTankTemperatureHigh rTankTemp rSetTemperature
# outside is good as long as tank temp is high enough
or rOutTempHigh rOutTempHigh rTankTemperatureHigh

# tank pressure too low?
mul rSetPressure cTankPressure 1000
slt rTankPressureLow rTankPressure rSetPressure

# tank pressure too high?
add rSetPressure rSetPressure cOverPressure
sgt rTankPressureHigh rTankPressure rSetPressure

# only add over temperature if tank not full enough
brne rTankPressureLow 1 2
add rSetTemperature rSetTemperature cOverTemperature

# tank temperature too high? release some gas
sgt rPumpToOutside rTankTemp rSetTemperature

# or if we are over pressuring the tank
or rPumpToOutside rPumpToOutside rTankPressureHigh

# enable vent if outside temperature is high enough
# and tank pressure too low
and rVent rOutTempHigh rTankPressureLow

jal update_state
yield
j start

update_state:
s dVent On rVent
s db Setting rVent

brdns dDiode 3
s dDiode On rVent
s dDiode Setting 1

s dPumpToOutside On rPumpToOutside

j ra